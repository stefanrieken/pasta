define "cursor" screen;

define "button" {
    args "text";
    set "cursor" (write cursor "\x08\x0c\x0c\x0c\x0c\x0c\x0c");
    setb cursor 0x0a; add "cursor" 1; # can't write graphical char under 'newline' value
    set "cursor" (write cursor "\n\x0d" text "\x0d\n");
    set "cursor" (write cursor "\x18\x0c\x0c\x0c\x0c\x0c\x0c\x1a\n")
}

button "save  "
button "page 0"
button "color "
button "pal  \x04"

define "sheet" (* 31 1024)
define "sheet_spr" (sprite 1)
setallb sheet_spr 64 0 8 8 (lsb sheet) (msb sheet) 0 0xA0 0x10 0x32 # set transparent=none; # default color scheme

# Fill the empty pages with a raster
define "i" 0x5871 # tilemem
loop {
    define "j" 0;
    loop {
        setb i j 1;
        add "j" 2;
        < j 16
    };
    add "i" 128;
    < i 0x6000
}

define "colorref" (+ sheet 256)
write colorref "0"
define "color_spr" (sprite 2)
setallb color_spr 48 56 1 1 (lsb colorref) (msb colorref) 0 0x00 0x10 0x32

# Show the full palette as squares; this takes 4 sprites and a useful video mode
define "palettemap" (+ colorref 1)
setallb palettemap 0b00000000 0b01000000 0b10000000 0b11000000

define "fullpalette_spr0" (sprite 3)
setallb fullpalette_spr0 64 208 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0x10 0x32
define "fullpalette_spr1" (sprite 4)
setallb fullpalette_spr1 128 208 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0x54 0x76
define "fullpalette_spr2" (sprite 5)
setallb fullpalette_spr2 64 224 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0x98 0xBA
define "fullpalette_spr3" (sprite 6)
setallb fullpalette_spr3 128 224 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0xDC 0xFE

# Show the 4-color palettes using a similar technique (and the same map)
define "palette_spr0" (sprite 7)
setallb palette_spr0 0 96 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0x10 0x32
define "palette_spr1" (sprite 8)
setallb palette_spr1 0 112 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0x54 0x76
define "palette_spr2" (sprite 9)
setallb palette_spr2 0 128 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0x98 0xBA
define "palette_spr3" (sprite 10)
setallb palette_spr3 0 144 4 1 (lsb palettemap) (msb palettemap) 2 0x50 0xDC 0xFE

define "page" {
    args "n";
    define "i" 0;
    loop {
        setb sheet i (+ n i);
        add "i" 1;
        <= i 64
    };

    draw
}

page 0

define "tilemem" 0x5000
define "color_idx" 0b00

define "adjust_colorref" {
    set_sprite_color color_spr 0 (get_sprite_color sheet_spr color_idx);
    if (= color_idx 0) { set_sprite_color color_spr 1 (get_sprite_color sheet_spr 1) }  # don't have the same background (color 0) as foreground color
    { set_sprite_color color_spr 1 0 }; # in other cases, use color 0
}

loop {
    if (return click) {
        if (&& (>= pointer_x 64) (< pointer_y 192)) {
            define "x" (/ (- pointer_x 64) 3); # offset on screen
            define "y" (/ pointer_y 3); # and scale

            define "byte" (+ (* y 16) (/ x 4));
            define "bits" (* (% x 4) 2);
            define "mask" (<< 0b11 (- 6 bits));
            define "value"  (<< color_idx (- 6 bits));

            #prints "painting"; print byte;
            setb tilemem (* (getb sheet) 16) byte (| (& (getb tilemem (* (getb sheet) 16) byte) (~ mask)) value);

            draw (+ (* x 3) 64) (* y 3) 3 3;  # redraw affected area only
        };
        if (&& (< pointer_x 64) (>= pointer_y 0) (< pointer_y 24)) {
            set "click" 0;
            save_sheet (+ tilemem (* (getb sheet) 16)) palette;
            setallb (+ screen 0x25) 0x64 0x21; #say 'saved'
            draw;
            setallb (+ screen 0x25) 0 0 # clear for next repaint
        };
        if (&& (< pointer_x 64) (>= pointer_y 24) (< pointer_y 48)) {
            set "click" 0; # stop repeats
            page (+ (getb sheet) 64);
            setb (+ screen 0x86) (+ 0x30 (/ (getb sheet) 64));
            draw
        };
        if (&& (< pointer_x 64) (>= pointer_y 48) (< pointer_y 72)) {
            set "click" 0; # stop repeats
            set "color_idx" (& (+ color_idx 1) 0b11); # Adjust active color.
            setb colorref (+ 48 color_idx); # Set color number (ascii '0' = 48)
            adjust_colorref;
            draw 48 56 8 8 # redraw color_idx sprite only
        };
        if (&& (< pointer_x 64) (>= pointer_y 72) (< pointer_y 96)) {
            set "click" 0; # stop repeats
            # Rotate the color palettes using sheet_spr as extra bucket
            setb sheet_spr colors0 (getb palette_spr3 colors0);
            setb sheet_spr colors1 (getb palette_spr3 colors1);
            setb palette_spr3 colors0 (getb palette_spr2 colors0);
            setb palette_spr3 colors1 (getb palette_spr2 colors1);
            setb palette_spr2 colors0 (getb palette_spr1 colors0);
            setb palette_spr2 colors1 (getb palette_spr1 colors1);
            setb palette_spr1 colors0 (getb palette_spr0 colors0);
            setb palette_spr1 colors1 (getb palette_spr0 colors1);
            setb palette_spr0 colors0 (getb sheet_spr colors0);
            setb palette_spr0 colors1 (getb sheet_spr colors1);
            adjust_colorref;
            draw
        };
        if (&& (>= pointer_x 64) (< pointer_x 192) (>= pointer_y 208) (< pointer_y 240)) {
            set "click" 0; # stop repeats
            define "colornum" (+ (/ (- pointer_x 64) 16) (* (/ (- pointer_y 208) 16) 8));
            set_sprite_color sheet_spr color_idx colornum;
            set_sprite_color palette_spr0 color_idx colornum;
            adjust_colorref;
            draw
        }
    } {
         if (&& (>= pointer_x 64) (< pointer_y 192)) {
            define "x" (/ (- pointer_x 64) 3); # offset on screen
            define "y" (/ pointer_y 3); # and scale

            setb (+ screen 1019) (+ (/ x 10) 48); # 48 = '0'
            setb (+ screen 1020) (+ (% x 10) 48);
            setb (+ screen 1021) 47; # '/'
            setb (+ screen 1022) (+ (/ y 10) 48);
            setb (+ screen 1023) (+ (% y 10) 48);
            draw 215 247 40 8 # redraw affected area only
        };
    };
    catchup
}

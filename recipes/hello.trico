# Assumes lib.trico is loaded
# Interpreter can autorun "run" function

define "cursor" (write screen "\x7FTricolore\n\n")
add "cursor" 1;
set "cursor" (write cursor "\x0FHello\x10, world!\n")

define "picked" 0
write (+ screen 252) ($ picked 10 4)

# Do a few bricks
add "cursor" 1; # leave a hole
loop {
    # set "cursor" (write cursor "\x1f"); # equivalent
    setb cursor 31;
    set "cursor" (+ 1 cursor);
    % cursor 16
}

# Add a sprite
define "flower" (sprite 1)
define "tile1" (+ screen 256)
setallb flower 120 16 1 1 (lsb tile1) (msb tile1) 0 0x01 0xE0 0x7F
setb tile1 28

define "cat" (sprite 2)
define "tile2" (+ tile1 1)
setallb cat 112 0 1 1 (lsb tile2) (msb tile2) 0 0x01 0x10 0x30 # set scale = 1x on both axes + set first color to be transparent
setb tile2 21

# Also register the single tile sprites for saving;
# Sprite memory register is section 6; top-of registers are from byte 16
setw (+ 16 12) (+ tile2 1)

draw

#bitfield "north" "east" "south" "west";

define "pxtouches" (bind {
    args "x" "y";
    set "x" (% x 128);
    set "y" (% y 128);
    # if y = 8-15, tile num should be 8; if 16-23, 16; if 24-31, 24
    # if x = 8-15, tile num should be +1
    define "tile" (+ (* (/ y 8) 16) (/ x 8));
    define "map" (| (<< (getb screen_spr map1) 8) (getb screen_spr map0));
    getb map tile
});

define "run" (bind {
    define "diffx"  0;
    define "diffy"  0;

    draw;

    loop {
        if (&& (= (getb cat xpos) (getb flower xpos)) (= (getb cat ypos) (getb flower ypos))) {
            beep 440 100; beep 660 100;
            if (= (getb flower xpos) 120) { setallb flower 8 120 } { setallb flower 120 16 };
            add "picked" 1;
            write (+ screen 252) ($ picked 10 4)
        };

        # X pos
        if (& direction 0b1100) {
            # Accelerate
            add "diffx" (- (>> direction 2) 2);
            # Maximize speed
            if (= diffx 0x0020) { set "diffx" 0x001F};
            if (= diffx 0xFFE0) { set "diffx" 0xFFF0}
        } {
            # Decelerate
            if (>= diffx 0x7FFF) { # = negative value
                add "diffx" 1
            } { if (> diffx 0) {
                sub "diffx" 1
            }};
        };

        # Y pos
        if (& direction 0b0011) {
            # Accelerate
            if (& direction 0b10) {
                add "diffy" 1 # Down
            } {
                if (&& (= diffy 0) (||
                    (pxtouches    (getb cat xpos)    (+ (getb cat ypos) 8))
                    (pxtouches (+ (getb cat xpos) 7) (+ (getb cat ypos) 8))
                ) ) {
                    set "diffy" 0xFFF6; # Jump start
                } {
                    add "diffy" 1 # Gravitate
                }
            }
        } {
            # No arrow keys active

            add "diffy" 1; # Gravitate
        };

        # Maximize speed
        if (= diffy 0x0020) { set "diffy" 0x001F};
        if (= diffy 0xFFE0) { set "diffy" 0xFFF0};

        # Correct overshooting of Y
        if (< diffy 0x7FFF) { # South
            loop { && (> diffy 0) (||
                (pxtouches (+ (getb cat xpos) 0) (+ (getb cat ypos) (/ diffy 4) 7))
                (pxtouches (+ (getb cat xpos) 7) (+ (getb cat ypos) (/ diffy 4) 7))
            ) } {
                sub "diffy" 4
            }
        } { # North
            loop { && (> diffy 0) (||
                (pxtouches (+ (getb cat xpos) 0) (+ (getb cat ypos) (/ diffy 4)))
                (pxtouches (+ (getb cat xpos) 7) (+ (getb cat ypos) (/ diffy 4)))
            ) } {
                add "diffy" 4
            }
        };

        # Correct overshooting of X
        if (< diffx 0x7FFF) { # East
            loop { && (> diffx 0) (||
                (pxtouches (+ (getb cat xpos) (/ diffx 4) 7) (+ (getb cat ypos) (/ diffy 4)))
                (pxtouches (+ (getb cat xpos) (/ diffx 4) 7) (+ (getb cat ypos) (/ diffy 4) 7))
            ) } {
                sub "diffx" 4
            }
        } { # West
            loop { && (> diffx 0) (||
                (pxtouches (+ (getb cat xpos) (/ diffx 4)) (+ (getb cat ypos) (/ diffy 4)))
                (pxtouches (+ (getb cat xpos) (/ diffx 4)) (+ (getb cat ypos) (/ diffy 4) 7))
            ) } {
                add "diffx" 4
            }
        };

        if (| (/ diffy 4) (/ diffx 4)) {
            # On 256x256, sprite coordinates rotate automatically as byte overflows
            # On 128x128, we must manually rotate the sprite at end of screen
            setb cat ypos (% (+ (getb cat ypos) (/ diffy 4)) 128);
            setb cat xpos (% (+ (getb cat xpos) (/ diffx 4)) 128);
            draw
        };

        catchup
    }
})

